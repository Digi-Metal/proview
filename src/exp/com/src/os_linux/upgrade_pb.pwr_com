! 
!  Proview   $Id: upgrade_pb.pwr_com,v 1.2 2005-09-01 14:57:49 claes Exp $
!  Copyright (C) 2005 SSAB Oxelösund AB.
!
!  This program is free software; you can redistribute it and/or 
!  modify it under the terms of the GNU General Public License as 
!  published by the Free Software Foundation, either version 2 of 
!  the License, or (at your option) any later version.
!
!  This program is distributed in the hope that it will be useful 
!  but WITHOUT ANY WARRANTY; without even the implied warranty of 
!  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the 
!  GNU General Public License for more details.
!
!  You should have received a copy of the GNU General Public License 
!  along with the program, if not, write to the Free Software 
!  Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
!
!
! Script for converting Profibus objects during upgrading from v3.9 to v4.0.0
!
! Traverse all trees under every $NodeHier root object to find every Pb_DP_Slave object.
! Attribute ByteOrdering has a new meaning and should be changed, was formerly bitmask.
! The new ByteOrdering in Pb_DP_Slave object is ONLY ByteOrdering
!
! Every Pb_Di, Pb_Do, Pb_Ai and Pb_Ao object also has new arguments, Orientation and 
! NumberRepresentation that should be set according to the old ByteOrdering bitmask.
! 
! Pb_Ii and Pb_Io are new objects in v4.0.0 and don't need changing.
!
function int convert(string slave)
  int old;
  int sts;
  string attr;
  string child;
  string class;
    
  attr = slave + ".ByteOrdering";
  old = GetAttribute(attr, sts);
  
  if (sts != 0) 

    child = GetChild(slave);
    while (child != "")
      class = GetObjectClass(child);
      if (class == "Pb_Di" || class == "Pb_Do")
        if (old & 2)
          set attr/attr=Orientation/value=16/name='child'/noco	  
	else
          set attr/attr=Orientation/value=8/name='child'/noco	  
	endif
      endif
      if (class == "Pb_Ai" || class == "Pb_Ao")
        if (old & 4)
          set attr/attr=NumberRepresentation/value=0/name='child'/noco	  
	else
          set attr/attr=NumberRepresentation/value=1/name='child'/noco	  
	endif
      endif
      child = GetNextSibling(child);
    endwhile
    
    if (old & 1)
      set attr/attr=ByteOrdering/value=1/name='slave'/noco
    else
      set attr/attr=ByteOrdering/value=0/name='slave'/noco   
    endif
 
    save
  endif
  
endfunction

function int traverse(string parent)
  string child;
  string class;
  
  child = GetChild(parent);
  while(child != "")
    class = GetObjectClass(child);
    if (class == "Pb_DP_Slave")
      convert(child);
    else
      traverse(child);
    endif
    child = GetNextSibling(child);
  endwhile
  
endfunction

main()
  string object;
  string class;
  ! 
  object = GetRootList();
  while (object != "")
    class = GetObjectClass(object);
    if (class == "$NodeHier")
      traverse(object);
    endif
    object = GetNextSibling(object);
  endwhile
endmain
